buildscript {
    ext {
        kotlinVersion = '1.0.3'
        springBootVersion = '1.4.1.RELEASE'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:${kotlinVersion}")
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '3.1'
}

ant.importBuild 'build.xml'

apply plugin: 'kotlin'
apply plugin: 'idea'
apply plugin: 'application'
apply plugin: 'jacoco'

sourceCompatibility = 1.8
targetCompatibility = 1.8

mainClassName = "pl.weblang.WeblangKt"

jacocoTestReport {
    reports {
        xml.enabled true
    }
}

jar {
    baseName = 'weblang'
    version = '0.0.1-SNAPSHOT'
    manifest {
        attributes("Implementation-Version": version)
    }

    outputs.upToDateWhen { false }

    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
}

task patchJar(type: Exec, dependsOn: jar) {
    commandLine './script/patchJar.sh'

    outputs.upToDateWhen { false }
}

repositories {
    mavenCentral()
    maven {
        url 'http://dl.bintray.com/omegat-org/maven'
    }
}


dependencies {
    compile("org.jetbrains.kotlin:kotlin-stdlib:${kotlinVersion}")

    // OmegaTÂ 
    compile('org.omegat:omegat:4.0.0-02') { transitive = false }
    compile 'org.omegat:vldocking:3.0.5'
}

patchJar.mustRunAfter jar
startScripts.dependsOn patchJar

